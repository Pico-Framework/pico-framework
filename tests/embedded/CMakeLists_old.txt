cmake_minimum_required(VERSION 3.13)

set(PICO_BOARD pico2_w)
project(Embedded_Tests C CXX ASM)

set(CMAKE_C_STANDARD 11)
set(CMAKE_CXX_STANDARD 17)

# Pico SDK and FreeRTOS
if(NOT DEFINED PICO_SDK_PATH)
    set(PICO_SDK_PATH "$ENV{HOME}/.pico-sdk/sdk/2.1.1" CACHE PATH "")
endif()

include(${CMAKE_CURRENT_SOURCE_DIR}/../../cmake/pico_sdk_import.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/../../cmake/FreeRTOS_Kernel_import.cmake)

# Framework & FreeRTOS-FAT-CLI paths
set(PICO_FRAMEWORK_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../..)
set(FRAMEWORK_DIR ${PICO_FRAMEWORK_DIR}/framework)
set(FREERTOS_FAT_CLI_DIR ${FRAMEWORK_DIR}/lib/FreeRTOS-FAT-CLI-for-RPi-Pico/src/FreeRTOS+FAT+CLI)

# Disable unnecessary features
set(PICO_NO_MBEDTLS TRUE)
set(PICO_HTTP_ENABLE_HTTP_CLIENT ON CACHE BOOL "" FORCE)

# Disable stdlib for CppUTest
set(CPPUTEST_USE_MEM_LEAK_DETECTION 0 CACHE BOOL "" FORCE)
set(CPPUTEST_USE_NEW_MACROS 0 CACHE BOOL "" FORCE)
set(CPPUTEST_USE_STD_CPP_LIB 0 CACHE BOOL "" FORCE)
set(CPPUTEST_USE_LONG_LONG 1 CACHE BOOL "" FORCE)
set(TESTS OFF CACHE BOOL "" FORCE)
set(EXAMPLES OFF CACHE BOOL "" FORCE)

# Init SDK
pico_sdk_init()

# Fetch CppUTest (embedded-compatible)
include(FetchContent)
FetchContent_Declare(
  CppUTest
  GIT_REPOSITORY https://github.com/cpputest/cpputest.git
  GIT_TAG        v4.0
  OVERRIDE_FIND_PACKAGE
  SOURCE_SUBDIR  .
)
FetchContent_MakeAvailable(CppUTest)

# Add framework
add_subdirectory(${FRAMEWORK_DIR} pico_framework)

# === Add FreeRTOS+FAT+CLI ===
file(GLOB FREERTOS_FAT_CLI_SOURCES
    ${FREERTOS_FAT_CLI_DIR}/*.c
    ${FREERTOS_FAT_CLI_DIR}/portable/RP2040/*.c
)

add_library(freertos_fat_cli STATIC ${FREERTOS_FAT_CLI_SOURCES})

target_include_directories(freertos_fat_cli PUBLIC
    ${FREERTOS_FAT_CLI_DIR}/include
    ${FREERTOS_FAT_CLI_DIR}/portable/RP2040
)

# === Embedded Unit Test Executable ===
add_executable(EmbeddedTest
    TestRunner.cpp
    test_StorageManager.cpp
    hw_config.c
    ${PICO_FRAMEWORK_DIR}/tests/host/mocks/test_stub_waitpid.c
)

target_include_directories(EmbeddedTest PRIVATE
    ${FRAMEWORK_DIR}/include
    ${FRAMEWORK_DIR}/include/port
    ${FREERTOS_FAT_CLI_DIR}/include
    ${FREERTOS_FAT_CLI_DIR}/portable/RP2040
    ${cpputest_SOURCE_DIR}/include
    ${cpputest_BINARY_DIR}
    ${CMAKE_CURRENT_SOURCE_DIR}  # for hw_config.h
)

target_compile_definitions(EmbeddedTest PRIVATE
    CPPUTEST_USE_LONG_LONG=1
    PICO_CXX_ENABLE_NEW_DELETE=0
    CPPUTEST_MEM_LEAK_DETECTION=1
    CPPUTEST_USE_STD_CPP_LIB=0
)

target_link_libraries(EmbeddedTest
    pico_framework
    CppUTest
    CppUTestExt
)

pico_enable_stdio_uart(EmbeddedTest 1)

# Prevent host test build
set_property(DIRECTORY "${cpputest_SOURCE_DIR}" PROPERTY EXCLUDE_FROM_ALL TRUE)
